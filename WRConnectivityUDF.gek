scn WRConnectivityUDF

begin function { }
	array_var aCells = GLTA 57
	array_var aCellsVisited = Ar_MapTo aCells ({array_var aIter} => 0)
    
    int iIndex = Ar_Size aCells
    while (iIndex -= 1) > -1
        ref rCell = aCells[iIndex]
        array_var aDoorsInCell = Ar_Filter (GetRefsInCell rCell 28 0 1) WRQuestStart.udfOnlyDoorRefs
        if eval (Ar_Size aDoorsInCell) > 0
            ref rDoor = aDoorsInCell[0]
            aCellsVisited[iIndex] = call WRIsConnectedToWorldspaceUDF rDoor
        else
            Ar_Erase aCells iIndex
            Ar_Erase aCellsVisited iIndex
        endif
    loop

	array_var aResult = Ar_Filter aCellsVisited ({array_var aIter} => *aIter == 0)
	PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "| Unconnected cells: " + $(Ar_Size aResult) + "/" + $(Ar_Size aCells))
    iIndex = -1
	while (iIndex += 1) < (Ar_Size aCells)
        if eval aCellsVisited[iIndex] == 0
            ref rCell = aCells[iIndex]
            array_var aDoors = Ar_Filter (GetRefsInCell rCell 28 0 1) WRQuestStart.udfOnlyDoorRefs
            int iDoors = Ar_Size aDoors
            PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "| " + RefToString rCell + " (" + GetEditorID rCell + ") (Teleport doors: " + $iDoors + ") Why?: ")
            foreach (array_var aEntry) <- aDoors
                ref rDoor = *aEntry
                if eval rDoor.GetDisabled
                    PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "|     " + rDoor.RefToString + " is disabled.")
                elseif eval rDoor.GetLockLevel == 255
                    if eval rDoor.GetKey
                        PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "|     " + rDoor.RefToString + " is locked by " + $(rDoor.GetOpenKey))
                    elseif eval rDoor.GetTeleportDoor.GetKey
                        PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "|     " + rDoor.RefToString + " is locked by " + $(rDoor.GetTeleportDoor.GetKey))
                    else
                        PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "|     " + rDoor.RefToString + " is locked by something.")
                    endif
                elseif eval GetFormFlag rDoor 256
                    PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "|     " + rDoor.RefToString + " is inaccessible.")
                elseif eval ListGetFormIndex WRProblemDoors (rDoor.GetTeleportDoor) != -1
                    PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "|     " + rDoor.RefToString + " is problematic.")
                elseif eval rDoor.GetTeleportDoor.GetTeleportDoor != rDoor
                    ref rLink = rDoor.GetTeleportDoor
                    PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "|     " + rDoor.RefToString + "'s link (" + rLink.RefToString + ") leads to " + rLink.GetTeleportDoor.RefToString + " instead of itself.")
                else
                    PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "|     " + rDoor.RefToString + " is weird.")
                endif
            loop
        endif
	loop
	
	SetFunctionValue (Ar_Size aResult)
end