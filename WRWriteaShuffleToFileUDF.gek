scn WRWriteaShuffleToFileUDF

begin function { }

	array_var aShuffle = GetArrayVar aShuffle WRQuestStart
	RemoveINISection "aShuffle" "RandomizedWorld.ini"
    WriteStringToFile "Data/config/RandomizedWorldDetailed.txt" 0 "%e"
	foreach (array_var aEntry) <- aShuffle
		string_var sKey = aEntry["key"]
		ref rValue = aEntry["value"]
		string_var srValue = rValue.RefToString ;Didn't like the name sValue for some reason????
		string_var sKeystring = "aShuffle/" + $sKey
		SetINIString $sKeystring $srValue "RandomizedWorld.ini"
        
        ref rKey = StringToRef $sKey
        string_var sKeyName = GetName rKey
        if eval sKeyName == "<no name>"
            sKeyName = GetName (rKey.GetBaseForm)
        endif
        string_var sValueName = GetName rValue
        if eval sValueName == "<no name>"
            sValueName = GetName (rValue.GetBaseForm)
        endif
        string_var sKeyParent = rKey.GetLocationName
        string_var sValueParent = rValue.GetLocationName
        PrintF "Data/config/RandomizedWorldDetailed.txt" $(sKey + ": " + sKeyName + " in " + sKeyParent + " goes to " + sValueName + " in " + sValueParent + " (" + srValue + ")")
	loop
	
	PrintF "WRLog.txt" $((call WRGetTimeStampUDF) + "| Created RandomizedWorld.ini and RandomizedWorldDetailed.txt.")
end