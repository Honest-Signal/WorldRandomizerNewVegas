scn WRVerifyCellAccessUDF

array_var aQueue
array_var aVisited
ref rCell

begin function { }
	SetFunctionValue 1
	
	int i = -1
	while eval (i += 1) < ListGetCount WRImportantCells
		rCell = ListGetNthForm WRImportantCells i ;aImportantCells[i]
		;printF "WRLog.txt" "Starting from " + $rCell + " - " + GetFormIdString rCell

		if rCell == TheFortWorld 
			if CaesarTentFlapInteriorREF.GetTeleportCell
				aVisited = Ar_List CaesarTentFlapInteriorREF.GetTeleportCell
				aQueue = Ar_List CaesarTentFlapInteriorREF.GetTeleportCell
			else
				;printF "WRVerifyCellAccessUDF.txt" "Path found!"
				continue
			endif
		else
			aVisited = Ar_List rCell
			aQueue = Ar_List rCell
		endif
	
		while (Ar_Size aQueue) > 0
			ref rSearch = aQueue[0]
			Ar_Erase aQueue 0
			;printF "WRLog.txt" $rSearch + " - " + GetFormIdString rSearch
			array_var aDoors = GetRefsInCell rSearch 28 0 1
			foreach array_var entry <- aDoors
				ref rDoor = *entry
				if eval rDoor.GetDisabled || rDoor.GetLockLevel == 255 || (GetFormFlag rDoor 256)
					continue
				endif
				if eval rDoor.GetTeleportDoor && ListGetFormIndex WRProblemDoors (rDoor.GetTeleportDoor) == -1 && rDoor.GetTeleportDoor.GetLockLevel != 255 && !(GetFormFlag (rDoor.GetTeleportDoor) 256)
					ref rNextCell = rDoor.GetTeleportCell ;Fun tidbit- Worldspace cells don't actually return from GetTeleportCell
					if eval rNextCell == 2CCampGolfTents || rNextCell == 2CCampGolfTents2
						continue
					endif
					if eval !(GetCellFlag rNextCell 0) ;Just doing this in case it's fixed someday but it could just be !rNextCell
							;printF "WRVerifyCellAccessUDF.txt" "Path found! " + GetFormIdString (rDoor.GetTeleportDoor) 
							aQueue = Ar_Null
							break
					elseif eval (Ar_Find rNextCell, aVisited) == Ar_BadNumericIndex
						Ar_Append aQueue, rNextCell
						Ar_Append aVisited, rNextCell
					endif
				endif
			loop
		loop

		;printF "WRVerifyCellAccessUDF.txt" "Ending search"
		if eval (Ar_Size aQueue) != -1
;			printF "WRLog.txt" "Couldn't find a path for " + $rCell
;			foreach array_var entry <- aDoors
;				ref rDoor = *entry
;				if rDoor.GetTeleportDoor
;					ref rLink = rDoor.GetTeleportDoor
;					printF "WRVerifyCellAccessUDF.txt" GetFormIdString rDoor + " GetDisabled: " + $rDoor.GetDisabled + " GetLockLevel: " + $rDoor.GetLockLevel + " Inaccessible: " + $(GetFormFlag rDoor 256)
;					printF "WRVerifyCellAccessUDF.txt" "  " + GetFormIdString rLink + " GetDisabled: " + $rLink.GetDisabled + " GetLockLevel: " + $rLink.GetLockLevel + " Inaccessible: " + $(GetFormFlag rLink 256)
;				endif
;			loop
			SetFunctionValue 0
			break
		endif
	loop
end